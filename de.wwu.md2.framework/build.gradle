dependencies {
	// To run MD2 creation workflow
	compile 'org.eclipse.equinox:org.eclipse.equinox.common:3.6.200.v20130402-1505'
	compile 'org.ow2.asm:asm:5.2'
	compile 'org.eclipse.emf:org.eclipse.emf.mwe2.launch:2.8.3'
	compile 'org.eclipse.xtext:org.eclipse.xtext:2.11.0'
	compile 'org.eclipse.xtext:org.eclipse.xtext.common.types:2.11.0'
	compile 'org.eclipse.xtext:org.eclipse.xtext.generator:2.11.0'
	compile 'org.eclipse.emf:org.eclipse.emf.mwe.utils:1.3.20.201605261059'
	compile 'org.eclipse.xpand:org.eclipse.xpand:2.0.0'
	compile 'org.eclipse.xpand:org.eclipse.xtend:2.0.0'
	compile 'org.eclipse.xpand:org.eclipse.xtend.typesystem.emf:2.0.0'
	
	// Dependencies to export as deployable plugin (currently manually managed in Eclipse project and /lib folder)
	//compile 'com.google.inject.extensions:guice-multibindings:3.0' // Current version is 4.1 but requires base guice-4.1 which does not work because Eclipse ships with guice-3.0 and will not load a second (newer) jar
	//compile 'commons-codec:commons-codec:1.10'
	// TODO -> runtime configuration?
}

eclipse {
	classpath {	
		file {
			whenMerged { classpath ->
				if(classpath.entries.findAll({entry -> entry instanceof org.gradle.plugins.ide.eclipse.model.Container && entry.path == 'org.eclipse.pde.core.requiredPlugins'
				}).size == 0) {
					
					withXml {
						logger.info 'Add classpath entry for org.eclipse.pde.core.requiredPlugins'
						
						def node = it.asNode()
						node.appendNode('classpathentry', [kind: 'con', path: 'org.eclipse.pde.core.requiredPlugins'])
					}
				}
				
				withXml {
					logger.info 'Add classpath entries for manually managed runtime dependencies'
					
					def node = it.asNode()
					node.appendNode('classpathentry', [kind: 'lib', exported: 'true', path: 'lib/commons-codec-1.10.jar'])
					node.appendNode('classpathentry', [kind: 'lib', exported: 'true', path: 'lib/guice-multibindings-3.0.jar'])
				}
				
				// Add source path
				if(classpath.entries.findAll({entry -> entry instanceof org.gradle.plugins.ide.eclipse.model.SourceFolder }).size == 0) {
					classpath.entries.add( new org.gradle.plugins.ide.eclipse.model.SourceFolder('src', null))
					classpath.entries.add( new org.gradle.plugins.ide.eclipse.model.SourceFolder('src-gen', null))
					classpath.entries.add( new org.gradle.plugins.ide.eclipse.model.SourceFolder('xtend-gen', null))
					classpath.entries.add( new org.gradle.plugins.ide.eclipse.model.SourceFolder('res', null)) // required as source folder at runtime to copy resources e.g. for backend
				}
				
				// Add Xtend_container (unknown if really necessary)
				// withXml {
				//	it.asNode().appendNode('classpathentry', [kind: 'con', path: 'org.eclipse.xtend.XTEND_CONTAINER'])
				//}
			}
		}
	}
}

task generateMD2LanguageArtifacts(type: JavaExec, dependsOn: 'eclipse') {
	main = 'org.eclipse.emf.mwe2.launch.runtime.Mwe2Launcher'
	classpath = configurations.compile + files('./src/') + files('./antlr-generator-3.2.0-patch.jar') // Cannot be set via sourceset as it throws errors before having generated the language files
	
	outputs.dir 'src-gen'
	args += 'src/de/wwu/md2/framework/GenerateMD2.mwe2'
}